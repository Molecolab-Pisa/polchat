! chpotpol.f90: A Polarisation consistent charge-fitting tool 
!               A Molecolab Tool www.dcci.unipi.it/molecolab/tools
!
! Copyright (C) 2014, 2015 
!   S. Caprasecca, C. Curutchet, S. Jurinovich, B. Mennucci
!
! This program is free software: you can redistribute it and/or modify
!   it under the terms of the GNU General Public License as published by
!   the Free Software Foundation, either version 3 of the License, or
!   (at your option) any later version.
!
! This program is distributed in the hope that it will be useful,
!   but WITHOUT ANY WARRANTY; without even the implied warranty of
!   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!   GNU General Public License for more details.
!
! A copy of the GNU General Public License can be found in LICENSE or at
!   <http://www.gnu.org/licenses/>.
!
!
! -------------------------------------------------------------------
!     Subroutine to compute the potential generated by a set of
!     charges and induced dipoles over the gridpoints.
!
Subroutine chpotpol(nch,ngr,Chg,DInv,Scr,RChGr,RChCh,R3,Vtot,DipInd)
  Implicit Real*8 (A-H,O-Z)
  DIMENSION Chg(nch),RChGr(4,ngr,nch),RChCh(4,nch,nch),Vtot(ngr),DInv(3*nch,3*nch)
  Dimension Scr(nch,nch), R3(nch,nch), DipInd(3)
  Dimension Ex(3*nch,nch),E(3*nch),Dip(3*nch),BxGrd(ngr,3*nch),Vdip(ngr),Vch(ngr)
  Parameter (Zero = 0.0d0, Small = 1.0d-10)
! Sanitise Rij3
  do i = 1, nch
    do j = 1, nch
      if (R3(i,j).lt.Small) R3(i,j)=Small
    enddo
  enddo
! Compute electric field due to charges
  Ex(1:nch,:)         = RChCh(1,:,:)*(Scr/(R3))
  Ex(nch+1:2*nch,:)   = RChCh(2,:,:)*(Scr/(R3))
  Ex(2*nch+1:3*nch,:) = RChCh(3,:,:)*(Scr/(R3))
  E = Matmul(Ex,Chg)
! Compute dipoles generated by the electric field
  Dip = Matmul(DInv,E)
! Compute potential on the grid generated by the dipoles
  BxGrd(:,1:nch)         = RChGr(1,:,:)/(RChGr(4,:,:)**3)
  BxGrd(:,nch+1:2*nch)   = RChGr(2,:,:)/(RChGr(4,:,:)**3)
  BxGrd(:,2*nch+1:3*nch) = RChGr(3,:,:)/(RChGr(4,:,:)**3)
  Vdip = Matmul(BxGrd,Dip)
! Compute potential on the grid generated by the charges
  Call chpot(nch,ngr,Chg,RChGr,Vch)
  Vtot = Vch + Vdip
! Compute total induced dipole
  do i = 1, 3
    DipInd(i) = sum(Dip((i-1)*nch+1:i*nch))
  enddo
!
  Return
End Subroutine
